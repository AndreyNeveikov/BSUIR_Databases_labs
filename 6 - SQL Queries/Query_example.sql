USE Склад_118
GO

/****************************************************************************************/
/*Различные варианты команды выборки данных из таблиц*/
/****************************************************************************************/

/* выборка всех столбцов и всех строк таблицы Регион */
 SELECT * 
 FROM Регион 

  /* выборка некоторых столбцов и всех строк (вертикальный фильтр) */
 SELECT Город, Адрес, Факс 
 FROM Регион 

  /* выборка всех столбцов и некоторых строк (горизонтальный фильтр) */
 SELECT * 
 FROM Регион 
 WHERE Страна = 'Беларусь' AND Город != 'Минск'

  /* выборка некоторых столбцов и некоторых строк (вертикальный 
      и горизонтальный фильтры) */
 SELECT Город, Адрес, Факс 
 FROM Регион 
 WHERE Страна = 'Беларусь' AND Город != 'Минск'

  /* выборка с сортировкой строк по столбцу Город, а при совпадении городов – по
      столбцу Адрес */
 SELECT * 
 FROM Регион
 ORDER BY Город, Адрес 

  /* выборка из двух таблиц путем их внутреннего соединения по столбцу КодРегиона */
 SELECT Поставщик.ИмяПоставщика, Регион.Город, Регион.Факс,
   Поставщик.КодПоставщика
 FROM Регион 
   INNER JOIN Поставщик ON Регион.КодРегиона =
     Поставщик.КодРегиона
 ORDER BY Поставщик.ИмяПоставщика  

  /* выборка данных из трех таблиц */
 SELECT Клиент.ИмяКлиента, Регион.Город, Регион.Факс,
   Заказ.Количество, Заказ.ДатаЗаказа
 FROM Регион 
   INNER JOIN Клиент ON Регион.КодРегиона = Клиент.КодРегиона
   INNER JOIN Заказ ON Клиент.КодКлиента = Заказ.КодКлиента  
 WHERE Заказ.Количество >= 2
 ORDER BY Клиент.ИмяКлиента, Заказ.ДатаЗаказа DESC  


 /* та же операция выборка данных из трех таблиц с использованием псевдонимов таблиц */
 SELECT К.ИмяКлиента, Р.Город, Р.Факс, З.Количество, З.ДатаЗаказа
 FROM Регион Р 
   INNER JOIN Клиент К  ON  Р.КодРегиона = К.КодРегиона
   INNER JOIN Заказ З  ON  К.КодКлиента = З.КодКлиента  
 WHERE З.Количество >= 2
 ORDER BY К.ИмяКлиента, З.ДатаЗаказа DESC  

  /* в SQL Server допускается опускать имена псевдонимов таблиц для тех столбцов, имена которых уникальны в пределах объединяемых таблиц. Поэтому предыдущую операцию выборки данных можно записать еще и так: */
 SELECT ИмяКлиента, Город, Факс, Количество, ДатаЗаказа
 FROM Регион Р 
   INNER JOIN Клиент К  ON  Р.КодРегиона = К.КодРегиона
   INNER JOIN Заказ З  ON  К.КодКлиента = З.КодКлиента  
 WHERE Количество >= 2
 ORDER BY ИмяКлиента, ДатаЗаказа DESC  

  /* выборка данных с формированием вычисляемого столбца Стоимость */
 SELECT Товар.Наименование, Товар.Цена, Заказ.Количество, 
   Товар.ЕдиницаИзм, Товар.Цена * Заказ.Количество AS Стоимость
 FROM Товар 
   INNER JOIN Заказ ON Товар.КодТовара = Заказ.КодТовара  
 ORDER BY Стоимость  

  /* подсчет итоговых данных для столбца Количество в таблице Заказ */
 SELECT 
   SUM(Количество) AS [Общее кол-во], AVG(Количество) AS Среднее, 
   MAX(Количество) AS Максимум, MIN(Количество) AS Минимум
 FROM Заказ  

  /* выборка данных с их группировкой по столбцу КодТовара и подсчетом для 
      каждой группы итоговых данных */
 SELECT КодТовара, 
   SUM(Количество) AS [Общее кол-во], AVG(Количество) AS Среднее, 
   MAX(Количество) AS Максимум, MIN(Количество) AS Минимум
 FROM Заказ  
 GROUP BY КодТовара  

/* предыдущая операция выборки данных c группировкой, дополненная условием отбора данных (предложение WHERE) и условием отбора итоговых данных (предложение HAVING)  */
 SELECT КодТовара, 
   SUM(Количество) AS [Общее кол-во], AVG(Количество) AS Среднее, 
   MAX(Количество) AS Максимум, MIN(Количество) AS Минимум
 FROM Заказ  
 WHERE Количество > 5
 GROUP BY КодТовара
 HAVING SUM(Количество) < 30  

 /* выборка данных из представления Запрос1 */
 SELECT * 
 FROM Запрос1
 WHERE ЕдиницаИзм = 'штука'


/****************************************************************************************/
/*Выборки некоторых системных данных*/
/****************************************************************************************/

  /* Список учетных записей, которым разрешен доступ к серверу */
 USE master			-- переключаемся на системную базу данных master
 SELECT name, dbname, password, language
 FROM syslogins 
 USE Склад_118			-- переключаемся обратно на базу данных Склад_ХХХ

  /* Список учетных записей, включенных в фиксированные роли сервера */
 EXEC sp_helpsrvrolemember

  /* Список пользователей базы данных Склад_ХХХ */
 EXEC sp_helpuser

  /* Список ролей (как фиксированных, так и пользовательских) базы данных Склад_ХХХ */
 EXEC sp_helprole

  /* Членство ролей и пользователей в ролях базы данных Склад_ХХХ */
 EXEC sp_helprolemember


/****************************************************************************************/
/*Синтаксис команды обновления данных в таблице или представлении*/
/****************************************************************************************/

/*В таблице Клиент заменить все значения NULL в столбце КодРегиона на значение 301 */
UPDATE Клиент
SET КодРегиона = 301
WHERE КодРегиона IS NULL 


/****************************************************************************************/
/*Синтаксис команды удаления данных из таблицы или представления*/
/****************************************************************************************/

SET DATEFORMAT dmy		-- задаем привычный формат даты: день.месяц.год

/*В таблице Заказ удалить все строки, в которых значение поля СрокПоставки не относится к текущему году*/
 DELETE FROM Заказ
 WHERE СрокПоставки < '01.01.2013' 
 GO


/****************************************************************************************/
/*Синтаксис команды изменения структуры таблицы*/
/****************************************************************************************/

/* Добавление новых полей ГОСТ и Размер в таблицу Товар */
 ALTER TABLE Товар ADD ГОСТ VARCHAR(20) NULL
 ALTER TABLE Товар ADD Размер INT NULL
 GO

  /* Изменение типа данных поля Размер в таблице Товар с INT на TINYINT */
 ALTER TABLE Товар ALTER COLUMN Размер TINYINT NULL
 GO

  /* Установка проверочного ограничения для поля Размер в таблице Товар */
 ALTER TABLE Товар ADD CONSTRAINT CK_Товар_Размер 
 CHECK (Размер BETWEEN 36 AND 46)
 GO

  /* Удаление столбца ГОСТ из таблицы Товар */
 ALTER TABLE Товар DROP COLUMN ГОСТ
 GO

  /* Удаление столбца Размер из таблицы Товар (сначала удаляется проверочное
      ограничение) */
 ALTER TABLE Товар DROP CONSTRAINT CK_Товар_Размер
 ALTER TABLE Товар DROP COLUMN Размер
 GO

  /* Удаление ограничения внешнего ключа FK_Товар_Валюта из таблицы Товар */
 ALTER TABLE Товар DROP CONSTRAINT  FK_Товар_Валюта

  /* Добавление ограничения внешнего ключа FK_Товар_Валюта в таблицу Товар */
 ALTER TABLE Товар ADD CONSTRAINT  FK_Товар_Валюта  FOREIGN KEY
   (КодВалюты)  REFERENCES  Валюта  ON UPDATE CASCADE
 GO


/****************************************************************************************/
/*Синтаксис команды удаления таблицы*/
/****************************************************************************************/

 /* Информация о связях родительской таблицы Товар с ее дочерними таблицами */
 EXEC sp_fkeys 'Товар'
 GO
 /*Сделан вывод: у таблицы Товар имеется одна дочерняя таблица – таблица Заказ*/

 /* Информация о связях дочерней таблицы Заказ с ее родительскими таблицами */
 EXEC sp_fkeys @fktable_name = 'Заказ'
 GO

  /* Удаление из базы данных таблицы Товар */
 ALTER TABLE Заказ DROP CONSTRAINT FK_Заказ_Товар	-- первая команда
 DROP TABLE Товар     							    -- вторая команда
 GO
/*
Сначала была разорвана связь между таблицами Товар и Заказ
путем удаления в Заказ ограничения внешнего ключа FK_Заказ_Товар. 
Только после этого была удалена таблица Товар.
*/



